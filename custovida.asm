.data
aluguel_grande: .float 1574.0 2441710.0 748789.0 13857.0 2168.0 16800.0 21464.0 11278400.0 2174.0 1840.0 1157.0 3529.0 1225.0 2997.0 145053.0 5438.0 18560.0 2424.0 2750.0 8120.0 345938.0 4275.0 328690.0 720163.0 4181.0 1874210.0 224369.0 2984.0 3511.0
aluguel_pequeno: .float 933.0 1457410.0 408261.0 7295.0 1749.0 10680.0 16311.0 4489560.0 1616.0 1108.0 839.0 1733.0 861.0 1850.0 42057.0 2509.0 13372.0 1493.0 1608.0 4126.0 249651.0 2998.0 225859.0 456428.0 2835.0 727013.0 105361.0 2235.0 2221.0
internet: .float 36.0 76572.0 25319.0 432.0 55.0 22754.0 1342.0 406331.0 164.0 52.0 21.0 28.0 27.0 62.0 1142.0 72.0 459.0 72.0 24.0 91.0 10661.0 257.0 17580.0 17591.0 93.0 30706.0 4676.0 64.0 91.0
utilitarios: .float 172.0 222032.0 61518.0 1125.0 207.0 670.0 2789.0 1726050.0 196.0 143.0 114.0 136.0 80.0 149.0 4912.0 224.0 1954.0 135.0 124.0 193.0 33176.0 364.0 31035.0 57302.0 479.0 118797.0 15623.0 147.0 178.0 
gasolina: .float 1.74 4096.0 1262.0 13.0 1.99 24.0 115.0 13107.0 3.81 1.10 1.74 1.46 1.59 0.94 52.0 0.95 23.0 1.47 1.88 8.0 302.0 2.27 707.0 1304.0 5.74 1677.0 168.0 1.48 2.80  
transporte_publico: .float 48.0 149500.0 40481.0 534.0 107.0 451.0 2409.0 187643.0 195.0 62.0 39.0 173.0 33.0 117.0 2572.0 192.0 856.0 130.0 84.0 249.0 10866.0 137.0 23155.0 40383.0 278.0 62588.0 9120.0 165.0 201.0
leite: .float 1.38 5119.0 1592.0 45.0 1.67 28.0 93.0 24292.0 7.0 1.05 0.95 1.11 1.10 1.27 91.0 1.71 22.0 3.29 1.51 14.0 248.0 6.0 1101.0 1143.0 5.72 2947.0 223.0 4.25 2.76
peito_frango: .float 6.0 11190.0 4152.0 118.0 7.0 68.0 214.0 33737.0 18.0 4.63 3.37 3.92 3.82 8.0 220.0 8.0 81.0 10.0 8.0 12.0 1505.0 18.0 2681.0 2867.0 13.0 3856.0 537.0 10.0 8.0
ovos: .float 4.28 9671.0 3329.0 76.0 7.0 51.0 157.0 31375.0 13.0 4.41 3.67 4.01 3.28 7.0 161.0 8.0 55.0 5.38 5.7 16.0 948.0 6.0 2089.0 3691.0 15.0 5729.0 325.0 6.0 11.0
medico: .float 56.0 109859.0 27217.0 547.0 76.0 905.0 1504.0 372006.0 215.0 93.0 70.0 55.0 57.0 231.0 2605.0 173.0 691.0 84.0 26.0 242.0 11410.0 195.0 36000.0 43782.0 351.0 32765.0 4514.0 94.0 54.0
antibioticos: .float 5.27 34723.0 14678.0 100.0 20.0 304.0 790.0 136404.0 76.0 18.0 7.0 7.0 8.0 28.0 445.0 55.0 104.0 27.0 6.0 46.0 1917.0 69.0 9654.0 11248.0 48.0 6336.0 1320.0 39.0 7.0
cinema: .float 25.0 30649.0 19786.0 319.0 33.0 190.0 991.0 106763.0 87.0 28.0 15.0 26.0 20.0 30.0 856.0 52.0 322.0 30.0 25.0 120.0 3967.0 124.0 7361.0 12201.0 74.0 29910.0 3905.0 36.0 40.0
academia: .float 36.0 131984.0 41838.0 988.0 67.0 1754.0 2160.0 473661.0 362.0 37.0 34.0 46.0 53.0 92.0 2752.0 325.0 581.0 63.0 34.0 389.0 22726.0 423.0 37193.0 53982.0 135.0 74403.0 9678.0 75.0 66.0
jantar: .float 46.0 69001.0 42252.0 826.0 70.0 532.0 1473.0 293368.0 126.0 62.0 35.0 40.0 42.0 65.0 2365.0 102.0 719.0 62.0 51.0 239.0 9775.0 194.0 18286.0 35639.0 126.0 31542.0 5056.0 82.0 70.0
salario_minimo: .float 1985.6 1623500.0 292446.0 6000.0 3531.20 7920.0 12500.0 5400000.0 2500.0 1160.0 1015.0 1806.0 1323.0 2640.0 22440.0 3520.0 28000.0 3784.0 1801.0 2420.0 425985.0 4000.0 346323.87 500000.0 1412.0 2096270.0 235840.0 3784.0 5170.0
tabela_precos: .word aluguel_grande, aluguel_pequeno, internet, utilitarios, gasolina, transporte_publico, leite, peito_frango, ovos, medico, antibioticos, cinema, academia, jantar, salario_minimo
tabela_moradia: .word aluguel_grande, aluguel_pequeno, internet, utilitarios
tabela_transporte: .word gasolina, transporte_publico
tabela_alimentacao: .word leite, peito_frango, ovos
tabela_saude: .word medico, antibioticos
tabela_lazer: .word cinema, academia, jantar
tabela_categorias: .word tabela_moradia, tabela_transporte, tabela_alimentacao, tabela_saude, tabela_lazer
tabela_quantidade_categorias: .word 4, 2, 3, 2, 3
custo_relativo: .float 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
indices_cidades: .word 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28

real: .float 1.0
dolar: .float 0.1737
euro: .float 0.1678
libra: .float 0.1398
iene: .float 26.3019
peso_colombiano: .float 711.7438
peso_argentino: .float 183.0496
libra_egipcia: .float 8.7336
dolar_australiano: .float 0.2768
peso_mexicano: .float 3.5727
taka: .float 21.1775
rupia: .float 2826.4556
boliviano: .float 1.1919
rublo: .float 16.8294
coroa_norueguesa: .float 1.9497
dolar_canadense: .float 0.2485
yuan: .float 1.2663
coroa_islandesa: .float 24.5761
riyal: .float 0.6516
colon: .float 87.9507 
peso_chileno: .float 167.1961
won: .float 251.8257
dolar_neozelandes: .float 0.3069

tabela_moedas: .word boliviano, colon, coroa_islandesa, coroa_norueguesa, dolar, dolar_australiano, dolar_canadense, dolar_neozelandes, euro, iene, libra, libra_egipcia, peso_argentino, peso_chileno, peso_colombiano, peso_mexicano, real, riyal, rublo, rupia, taka, won, yuan
tabela_moedas_paises: .word euro, peso_colombiano, peso_argentino, libra_egipcia, dolar_australiano, peso_mexicano, taka, rupia, boliviano, dolar, euro, libra, euro, dolar, rublo, dolar, coroa_norueguesa, dolar_canadense, euro, yuan, coroa_islandesa, riyal, colon, peso_chileno, real, won, iene, dolar_canadense, dolar_neozelandes
str1: .asciiz "\nDigite seu salario: R$"
str2: .asciiz "\nEm "
str3: .asciiz "\nPreco aluguel de 85m2: "
str4: .asciiz "\nPreco aluguel de 45m2: "
str5: .asciiz "\nPreco da internet(8 mbps): "
str6: .asciiz "\nPreco utilitarios(aquecimento, gas, energia): "
str7: .asciiz "\nPreco litro da gasolina: "
str8: .asciiz "\nPreco da passagem mensal de transporte publico: "
str9: .asciiz "\nPreco do litro do leite: "
str10: .asciiz "\nPreco 500g de peito de frango: "
str11: .asciiz "\nPreco cartela com 12 ovos: "
str12: .asciiz "\nPreco da consulta com medico particular: "
str13: .asciiz "\nPreco caixa de antibioticos: "
str14: .asciiz "\nPreco dois ingressos para o cinema: "
str15: .asciiz "\nMensalidade academia: "
str16: .asciiz "\nJantar basico para dois em restaurante do bairro: "
str17: .asciiz "\nSalario minimo: "
str_precos: .word str3, str4, str5, str6, str7, str8, str9, str10, str11, str12, str13, str14, str15, str16, str17
str_menu_principal: .asciiz "\n\nmenu de opcoes:\n0-Fechar programa\n1-Mostrar custo de vida de uma cidade\n2-Fazer conversao de moedas\n3-Comparar seu salario com o salario minimo de outra cidade\n4-Comparar custo de vida entre cidades\n5-Organizar cidades por menor custo relativo\n"
str_lista_cidades: .asciiz "0-Berlim\n1-Bogota\n2-Buenos Aires\n3-Cairo\n4-Camberra\n5-Cidade do Mexico\n6-Daca\n7-Jacarta\n8-La Paz\n9-Las Vegas\n10-Lisboa\n11-Londres\n12-Madrid\n13-Miami\n14-Moscou\n15-Nova York\n16-Oslo\n17-Ottawa\n18-Paris\n19-Pequim\n20-Reykjavik\n21-Riade\n22-San Jose\n23-Santiago\n24-Sao Paulo\n25-Seul\n26-Toquio\n27-Toronto\n28-Wellington\n"
str_menu_custo_vida: .asciiz "\nPara iniciar, digite o numero correspondente com a cidade que deseja:\n"
str_lista_moedas: .asciiz "0-Boliviano\n1-Colon\n2-Coroa Islandesa\n3-Coroa Norueguesa\n4-Dolar\n5-Dolar Australiano\n6-Dolar Canadense\n7-Dolar Neozelandes\n8-Euro\n9-Iene\n10-Libra\n11-Libra Egipcia\n12-Peso Argentino\n13-Peso Chileno\n14-Peso Colombiano\n15-Peso Mexicano\n16-Real\n17-Riyal\n18-Rublo\n19-Rupia\n20-Taka\n21-Won\n22-Yuan\n"
str_menu_conversao1: .asciiz "\nDigite o indice correspondente a moeda que deseja converter para outra:\n"
str_menu_conversao2: .asciiz "\nDigite o indice correspondente a moeda desejada:\n"
str_menu_conversao3: .asciiz "\nDigite a quantia que deseja converter: "
str_menu_comparacao0: .asciiz "\nDigite o indice da cidade que deseja comparar o salario minimo com o seu salario:\n"
str_menu_comparacao1: .asciiz "\nO salario minimo em "
str_menu_comparacao2: .asciiz " eh de "
str_menu_comparacao3: .asciiz ", o que equivale a R$"
str_menu_comparacao4: .asciiz ", ou seja, "
str_menu_comparacao5: .asciiz "% do seu salario"
str_menu_comparacao_cidades1: .asciiz "\nDigite o indice da primeira cidade:\n"
str_menu_comparacao_cidades2: .asciiz "\nDigite o indice da segunda cidade:\n"
str_menu_comparacao_cidades3: .asciiz " eh "
str_menu_comparacao_cidades4: .asciiz "% mais barato do que em "
str_menu_comparacao_cidades5: .asciiz "% mais caro do que em "

str_real: .asciiz " Reais"
str_dolar: .asciiz " Dolares"
str_euro: .asciiz " Euros"
str_libra: .asciiz " Libras"
str_iene: .asciiz " Ienes"
str_pesocolombiano: .asciiz " Pesos colombianos"
str_pesoargentino: .asciiz " Pesos argentinos"
str_libraegipcia: .asciiz " Libras egipcias"
str_dolaraustraliano: .asciiz " Dolares australianos"
str_pesomexicano: .asciiz " Pesos Mexicanos"
str_taka: .asciiz " Takas"
str_rupia: .asciiz " Rupias"
str_boliviano: .asciiz " Bolivianos"
str_rublo: .asciiz " Rublos russos"
str_coroanorueguesa: .asciiz " Coroas norueguesas"
str_dolarcanadense: .asciiz " Dolares canadenses"
str_yuan: .asciiz " Yuans chineses"
str_coroaislandesa: .asciiz " Coroas islandesas"
str_riyal: .asciiz " Riyais sauditas"
str_colon: .asciiz " Colons costarriquenhos"
str_pesochileno: .asciiz " Pesos chilenos"
str_won: .asciiz " wons sul-coreanos"
str_dolarneozelandes: .asciiz " Dolares neozelandeses"
str_moedas: .word str_boliviano, str_colon, str_coroaislandesa, str_coroanorueguesa, str_dolar, str_dolaraustraliano, str_dolarcanadense, str_dolarneozelandes, str_euro, str_iene, str_libra, str_libraegipcia, str_pesoargentino, str_pesochileno, str_pesocolombiano, str_pesomexicano, str_real, str_riyal, str_rublo, str_rupia, str_taka, str_won, str_yuan
str_moedas_paises: .word str_euro, str_pesocolombiano, str_pesoargentino, str_libraegipcia, str_dolaraustraliano, str_pesomexicano, str_taka, str_rupia, str_boliviano, str_dolar, str_euro, str_libra, str_euro, str_dolar, str_rublo, str_dolar, str_coroanorueguesa, str_dolarcanadense, str_euro, str_yuan, str_coroaislandesa, str_riyal, str_colon, str_pesochileno, str_real, str_won, str_iene, str_dolarcanadense, str_dolarneozelandes

str_berlim: .asciiz "Berlim"
str_bogota: .asciiz "Bogota"
str_buenosaires: .asciiz "Buenos Aires"
str_cairo: .asciiz "Cairo"
str_camberra: .asciiz "Camberra"
str_cidadedomexico: .asciiz "Cidade do Mexico"
str_daca: .asciiz "Daca"
str_jacarta: .asciiz "Jacarta"
str_lapaz: .asciiz "La Paz"
str_lasvegas: .asciiz "Las Vegas"
str_lisboa: .asciiz "Lisboa"
str_londres: .asciiz "Londres"
str_madrid: .asciiz "Madrid"
str_miami: .asciiz "Miami"
str_moscou: .asciiz "Moscou"
str_novayork: .asciiz "Nova York"
str_oslo: .asciiz "Oslo"
str_ottawa: .asciiz "Ottawa"
str_paris: .asciiz "Paris"
str_pequim: .asciiz "Pequim"
str_reykjavik: .asciiz "Reykjavik"
str_riade: .asciiz "Riade"
str_sanjose: .asciiz "San Jose"
str_santiago: .asciiz "Santiago"
str_saopaulo: .asciiz "Sao Paulo"
str_seul: .asciiz "Seul"
str_toquio: .asciiz "Toquio"
str_toronto: .asciiz "Toronto"
str_wellington: .asciiz "Wellington"
tabela_cidades: .word str_berlim, str_bogota, str_buenosaires, str_cairo, str_camberra, str_cidadedomexico, str_daca, str_jacarta, str_lapaz, str_lasvegas, str_lisboa, str_londres, str_madrid, str_miami, str_moscou, str_novayork, str_oslo, str_ottawa, str_paris, str_pequim, str_reykjavik, str_riade, str_sanjose, str_santiago, str_saopaulo, str_seul, str_toquio, str_toronto, str_wellington

str_moradia: .asciiz "\nMoradia: "
str_transporte: .asciiz "\nTransporte: "
str_alimentacao: .asciiz "\nAlimentacao: "
str_saude: .asciiz "\nSaude: "
str_lazer: .asciiz "\nLazer: "
str_total: .asciiz "\nTotal: "
str_tabela_categorias: .word str_moradia, str_transporte, str_alimentacao, str_saude, str_lazer, str_total

str_organizar: .asciiz "\nEm ordem crescente, os paises com menor custo relativo em relacao ao salario minimo sao:\n"
str_lista1: .asciiz "1-"
str_lista2: .asciiz "2-"
str_lista3: .asciiz "3-"
str_lista4: .asciiz "4-"
str_lista5: .asciiz "5-"
str_lista6: .asciiz "6-"
str_lista7: .asciiz "7-"
str_lista8: .asciiz "8-"
str_lista9: .asciiz "9-"
str_lista10: .asciiz "10-"
str_lista11: .asciiz "11-"
str_lista12: .asciiz "12-"
str_lista13: .asciiz "13-"
str_lista14: .asciiz "14-"
str_lista15: .asciiz "15-"
str_lista16: .asciiz "16-"
str_lista17: .asciiz "17-"
str_lista18: .asciiz "18-"
str_lista19: .asciiz "19-"
str_lista20: .asciiz "20-"
str_lista21: .asciiz "21-"
str_lista22: .asciiz "22-"
str_lista23: .asciiz "23-"
str_lista24: .asciiz "24-"
str_lista25: .asciiz "25-"
str_lista26: .asciiz "26-"
str_lista27: .asciiz "27-"
str_lista28: .asciiz "28-"
str_lista29: .asciiz "29-"
str_tabela_lista: .word str_lista1, str_lista2, str_lista3, str_lista4, str_lista5, str_lista6, str_lista7, str_lista8, str_lista9, str_lista10, str_lista11, str_lista12, str_lista13, str_lista14, str_lista15, str_lista16, str_lista17, str_lista18, str_lista19, str_lista20, str_lista21, str_lista22, str_lista23, str_lista24, str_lista25, str_lista26, str_lista27, str_lista28, str_lista29

str_indice_invalido: .asciiz "\nIndice invalido, tente novamente\n"
virgula: .asciiz ","
igual: .asciiz " = "
quebra: .asciiz "\n"
.text
.globl main

main:
    li $v0, 4
    la $a0, str1
    syscall
    
    li $v0, 6
    syscall
    mov.s $f5, $f0                          # $f5 = salario
    li $s3, 15                              # $s3 = quantidade itens
    li $s4, 29                              # $s4 = quantidade cidades
    li $s5, 23                              # $s5 = quantidade moedas     

menu_principal:    
    li $v0, 4
    la $a0, str_menu_principal
    syscall
    
    li $v0, 5
    syscall
    move $s1, $v0                       # $s1 = escolha menu principal
    
    beq $s1, 0, exit
    beq $s1, 1, inicio_custo_vida
    beq $s1, 2, conversao
    beq $s1, 3, comparar_salario
    beq $s1, 4, comparar_cidades
    beq $s1, 5, organizar_cidades
    
    li $v0, 4
    la $a0, str_indice_invalido
    syscall
    j menu_principal
    
inicio_custo_vida:
    li $v0, 4
    la $a0, str_menu_custo_vida
    syscall
    
    la $a0, str_lista_cidades
    syscall
    
    li $v0, 5
    syscall
    move $s2, $v0                           # $s2 = escolha pais
    blt $s2, 0, indice_custo_vida_invalido
    bge $s2, $s4, indice_custo_vida_invalido
    li.s $f0, 100.00

custo_vida:
    mul $s2, $s2, 4
    li $v0, 4
    la $a0, str2
    syscall
    
    la $t2, str_precos
    la $t3, tabela_precos
    la $t4, tabela_cidades
    add $t4, $t4, $s2
    lw $a0, 0($t4)
    syscall
    
    la $t7, str_moedas_paises
    add $t7, $t7, $s2
    
    loop:
        beq $t5, $s3, menu_principal
        
        li $v0, 4
        lw $a0, 0($t2)
        syscall
        
        lw $a0, 0($t3)
        add $a0, $a0, $s2
        l.s $f1, 0($a0)
        mul.s $f1, $f1, $f0
        cvt.w.s $f1, $f1
        mfc1 $t0, $f1
        
        li $t1, 100
        div $t0, $t1
        mfhi $t1
        mflo $t0
        
        li $v0, 1
        move $a0, $t0
        syscall
        
        li $v0, 4
        la $a0, virgula
        syscall
        
        bge $t1, 10, pos_virgula_custo_vida
        li $v0, 1
        li $a0, 0
        syscall
        
        pos_virgula_custo_vida:
        li $v0, 1
        move $a0, $t1
        syscall
        
        addi $t2, $t2, 4
        addi $t3, $t3, 4
        addi $t5, $t5, 1
        
        li $v0, 4
        lw $a0, 0($t7)
        syscall
        
        j loop
    
indice_custo_vida_invalido:
    li $v0, 4
    la $a0, str_indice_invalido
    syscall
    j inicio_custo_vida
    
conversao:
    li $v0, 4
    la $a0, str_menu_conversao1
    syscall
    
    li $v0, 4
    la $a0, str_lista_moedas
    syscall
    
    li $v0, 5
    syscall
    move $t8, $v0                                       # $t8 = indice primeira moeda
    
    li $v0, 4
    la $a0, str_menu_conversao2
    syscall
    
    li $v0, 4
    la $a0, str_lista_moedas
    syscall
    
    li $v0, 5
    syscall
    move $t9, $v0                                       # $t9 = indice segunda moeda
    
    li $v0, 4
    la $a0, str_menu_conversao3
    syscall
    
    li $v0, 6
    syscall
    mov.s $f1, $f0                                      # $f1 = Valor a ser convertido
    
    li $v0, 4
    la $a0, quebra
    syscall
    
    blt $t8, 0, indice_conversao_invalido
    bge $t8, $s5, indice_conversao_invalido
    blt $t9, 0, indice_conversao_invalido
    bge $t9, $s5, indice_conversao_invalido
    
    li.s $f3, 100.0
    mul.s $f2, $f1, $f3
    cvt.w.s $f2, $f2
    mfc1 $t0, $f2
    
    li $t2, 100
    div $t0, $t2
    mfhi $t4
    mflo $t5
    
    li $v0, 1
    move $a0, $t5
    syscall
    
    li $v0, 4
    la $a0, virgula
    syscall
    
    bge $t4, 10, pos_virgula_conversao1
    li $v0, 1
    li $a0, 0
    syscall
        
    pos_virgula_conversao1:
    li $v0, 1
    move $a0, $t4
    syscall
    
    la $t6, str_moedas
    mul $t3, $t8, 4
    add $t3, $t3, $t6
    lw $a0, 0($t3)
    li $v0, 4
    syscall
    
    li $v0, 4
    la $a0, igual
    syscall
    
    la $t0, tabela_moedas
    mul $t1, $t8, 4
    add $t1, $t1, $t0
    lw $a0, 0($t1)
    l.s $f4, 0($a0)
    mul $t3, $t9, 4
    add $t3, $t3, $t0
    lw $a0, 0($t3)
    l.s $f6, 0($a0)
    div.s $f1, $f1, $f4
    mul.s $f2, $f1, $f6
    
    mul.s $f2, $f2, $f3
    cvt.w.s $f2, $f2
    mfc1 $t4, $f2
    
    div $t4, $t2
    mfhi $t4
    mflo $t5
    
    li $v0, 1
    move $a0, $t5
    syscall
    
    li $v0, 4
    la $a0, virgula
    syscall
    
    bge $t4, 10, pos_virgula_conversao2
    li $v0, 1
    li $a0, 0
    syscall
    
    pos_virgula_conversao2:
    li $v0, 1
    move $a0, $t4
    syscall
    
    la $t0, str_moedas
    mul $t9, $t9, 4
    add $t0, $t0, $t9
    lw $a0, 0($t0)
    li $v0, 4
    syscall
    
    j menu_principal

indice_conversao_invalido:
    li $v0, 4
    la $a0, str_indice_invalido
    syscall
    j conversao

comparar_salario:
    li $v0, 4
    la $a0, str_menu_comparacao0
    syscall
    
    la $a0, str_lista_cidades
    syscall
    
    li $v0, 5
    syscall
    move $t0, $v0
    
    blt $t0, 0, indice_comparar_salario_invalido
    bge $t0, $s4, indice_comparar_salario_invalido
    la $t1, salario_minimo
    la $t2, tabela_moedas_paises
    la $t3, tabela_cidades
    la $t4, str_moedas_paises
    li.s $f4, 100.0
    li.s $f7, 1000.0
    mul $t0, $t0, 4
    add $t1, $t1, $t0
    add $t2, $t2, $t0
    add $t3, $t3, $t0
    add $t4, $t4, $t0
    l.s $f1, 0($t1)                                 # $f1 = valor em moeda local
    lw $t2, 0($t2)
    
    l.s $f2, 0($t2)                                 # $f2 = cotacao da moeda
    div.s $f3, $f1, $f2                             # $f3 = valor em reais
    div.s $f6, $f3, $f5
    mul.s $f6, $f6, $f7
    
    li $v0, 4
    la $a0, str_menu_comparacao1
    syscall
    
    lw $a0, 0($t3)
    syscall
    
    la $a0, str_menu_comparacao2
    syscall
    
    mul.s $f1, $f1, $f4
    cvt.w.s $f1, $f1
    mfc1 $t1, $f1
    li $t6, 100
    div $t1, $t6
    mfhi $t1
    mflo $t5
    
    li $v0, 1
    move $a0, $t5
    syscall
    
    li $v0, 4
    la $a0, virgula
    syscall
    
    bge $t1, 10, pos_virgula_comparacao1
    li $v0, 1
    li $a0, 0
    syscall
    
    pos_virgula_comparacao1:
    li $v0, 1
    move $a0, $t1
    syscall
    
    li $v0, 4
    lw $a0, 0($t4)
    syscall
    
    li $v0, 4
    la $a0, str_menu_comparacao3
    syscall
    
    mul.s $f3, $f3, $f4
    cvt.w.s $f3, $f3
    mfc1 $t2, $f3
    div $t2, $t6
    mfhi $t1
    mflo $t2
    
    li $v0, 1
    move $a0, $t2
    syscall
    
    li $v0, 4
    la $a0, virgula
    syscall
    
    li $v0, 1
    move $a0, $t1
    syscall
    
    li $v0, 4
    la $a0, str_menu_comparacao4
    syscall
    
    cvt.w.s $f6, $f6
    mfc1 $t7, $f6
    li $t1, 10
    div $t7, $t1
    mfhi $t1
    mflo $t2
    
    li $v0, 1
    move $a0, $t2
    syscall
    
    li $v0, 4
    la $a0, virgula
    syscall
    
    li $v0, 1
    move $a0, $t1
    syscall
    
    li $v0, 4
    la $a0, str_menu_comparacao5
    syscall
    
    j menu_principal
    
indice_comparar_salario_invalido:
    li $v0, 4
    la $a0, str_indice_invalido
    syscall
    j comparar_salario

comparar_cidades:
    li $v0, 4
    la $a0, str_menu_comparacao_cidades1
    syscall
    
    li $v0, 4
    la $a0, str_lista_cidades
    syscall
    
    li $v0, 5
    syscall
    move $s6, $v0                                   # $s6 = indice cidade 1
    
    blt $s6, 0, indice_invalido_comparar_cidades
    bge $s6, $s4, indice_invalido_comparar_cidades
    
    li $v0, 4
    la $a0, str_menu_comparacao_cidades2
    syscall
    
    li $v0, 4
    la $a0, str_lista_cidades
    syscall
    
    li $v0, 5
    syscall
    move $s7, $v0                                   # $s7 = indice cidade 2
    
    blt $s7, 0, indice_invalido_comparar_cidades
    bge $s7, $s4, indice_invalido_comparar_cidades
    
    beq $s6, $s7, indice_invalido_comparar_cidades
    
    mul $s6, $s6, 4
    mul $s7, $s7, 4
    
    la $s0, tabela_categorias
    la $t5, tabela_quantidade_categorias
    li $t0, 0
    li.s $f6, 0.0                               # $f6 = soma total cidade 1
    li.s $f7, 0.0                               # $f7 = soma total cidade 2
    la $t2, tabela_moedas_paises
    la $t8, tabela_cidades
    la $t9, str_tabela_categorias
    add $t3, $t2, $s6
    add $t4, $t2, $s7
    lw $t3, 0($t3)
    lw $t4, 0($t4)
    l.s $f8, 0($t3)
    l.s $f9, 0($t4)
    loop_primario:
        bge $t0, 5, fim_loop_primario
        lw $t1, 0($s0)
        addi $t0, $t0, 1
        li.s $f3, 0.0                               # $f3 = soma de categorias cidade 1
        li.s $f4, 0.0                               # $f4 = soma de categorias cidade 2
        lw $t6, 0($t5)
        li $t7, 0
        loop_secundario:
            bge $t7, $t6, fim_loop_secundario
            lw $t2, 0($t1)
            add $t3, $t2, $s6
            add $t4, $t2, $s7
            l.s $f1, 0($t3)                         # $f1 = Valor item cidade 1
            l.s $f2, 0($t4)                         # $f2 = Valor item cidade 2
            add.s $f3, $f3, $f1
            add.s $f4, $f4, $f2
            addi $t1, $t1, 4
            addi $t7, $t7, 1
            j loop_secundario
        fim_loop_secundario:
            # Transforma moeda cidade 1 em moeda cidade 2
            div.s $f3, $f3, $f8
            mul.s $f3, $f3, $f9
            # Soma no total
            add.s $f6, $f6, $f3
            add.s $f7, $f7, $f4
            
            # Comparacao
            sub.s $f3, $f3, $f4
            div.s $f4, $f3, $f4
            li.s $f10, 1000.0
            mul.s $f4, $f4, $f10
            cvt.w.s $f4, $f4
            mfc1 $t6, $f4
            li $t1, 10
            div $t6, $t1
            mfhi $t6
            mflo $t1
            
            li $v0, 4
            lw $a0, 0($t9)
            syscall
            
            add $a0, $t8, $s6
            lw $a0, 0($a0)
            syscall
            
            la $a0, str_menu_comparacao_cidades3
            syscall
            
            bgt $t1, 0, mais_caro
            
            mul $t1, $t1, -1
            mul $t6, $t6, -1
            
            li $v0, 1
            move $a0, $t1
            syscall
            
            li $v0, 4
            la $a0, virgula
            syscall
            
            li $v0, 1
            move $a0, $t6
            syscall
            
            li $v0, 4
            la $a0, str_menu_comparacao_cidades4
            syscall
            
            add $a0, $t8, $s7
            lw $a0, 0($a0)
            syscall
            
            # Atualiza valores indices
            addi $s0, $s0, 4
            addi $t5, $t5, 4
            addi $t9, $t9, 4
            j loop_primario
            
            mais_caro:
            
            li $v0, 1
            move $a0, $t1
            syscall
            
            li $v0, 4
            la $a0, virgula
            syscall
            
            li $v0, 1
            move $a0, $t6
            syscall
            
            li $v0, 4
            la $a0, str_menu_comparacao_cidades5
            syscall
            
            add $a0, $t8, $s7
            lw $a0, 0($a0)
            syscall
            
            # Atualiza valores indices
            addi $s0, $s0, 4
            addi $t5, $t5, 4
            addi $t9, $t9, 4
            j loop_primario
            
fim_loop_primario:
    li $v0, 4
    lw $a0, 0($t9)
    syscall
    
    add $a0, $t8, $s6
    lw $a0, 0($a0)
    syscall
    
    la $a0, str_menu_comparacao_cidades3
    syscall
    
    sub.s $f6, $f6, $f7
    div.s $f7, $f6, $f7
    li.s $f10, 1000.0
    mul.s $f7, $f7, $f10
    cvt.w.s $f7, $f7
    mfc1 $t6, $f7
    li $t1, 10
    div $t6, $t1
    mfhi $t6
    mflo $t1
    
    bgt $t1, 0, mais_caro2
 
    mul $t1, $t1, -1
    mul $t6, $t6, -1
 
    li $v0, 1
    move $a0, $t1
    syscall
 
    li $v0, 4
    la $a0, virgula
    syscall

    li $v0, 1
    move $a0, $t6
    syscall

    li $v0, 4
    la $a0, str_menu_comparacao_cidades4
    syscall

    add $a0, $t8, $s7
    lw $a0, 0($a0)
    syscall
    
    j menu_principal

    mais_caro2:

    li $v0, 1
    move $a0, $t1
    syscall

    li $v0, 4
    la $a0, virgula
    syscall
    
    li $v0, 1
    move $a0, $t6
    syscall
    
    li $v0, 4
    la $a0, str_menu_comparacao_cidades5
    syscall
    
    add $a0, $t8, $s7
    lw $a0, 0($a0)
    syscall
    
    j menu_principal

indice_invalido_comparar_cidades:
    li $v0, 4
    la $a0, str_indice_invalido
    syscall
    j comparar_cidades

organizar_cidades:
    la $t0, tabela_precos
    la $t1, indices_cidades
    la $t2, tabela_cidades
    la $t3, custo_relativo
    li $t4, 0
    li $t5, 0
    li.s $f1, 0.0
    addi $t8, $s3, -1
    loop_calcula_indice_soma:
        beq $t4, $t8, fim_loop_calcula_indice_soma
        mul $t6, $t4, 4
        add $t6, $t6, $t0
        lw $t6, 0($t6)
        mul $t7, $t5, 4
        add $t6, $t6, $t7
        l.s $f2, 0($t6)
        add.s $f1, $f1, $f2
        addi $t4, $t4, 1
        
        j loop_calcula_indice_soma
    fim_loop_calcula_indice_soma:
        bge $t5, $s4, bubble_sort
        mul $t7, $t4, 4
        add $t7, $t7, $t0
        lw $t7, 0($t7)
        mul $t6, $t5, 4
        add $t7, $t7, $t6
        l.s $f3, 0($t7)
        add $t6, $t6, $t3
        div.s $f1, $f1, $f3
        s.s $f1, 0($t6)
        addi $t5, $t5, 1
        li.s $f1, 0.0
        li $t4, 0
        
        j loop_calcula_indice_soma
    
    bubble_sort:
        li $t4, 0                               # $t4 = i
        li $t5, 0                               # $t5 = flag troca
        
        loop_externo_bubble_sort:
            sub $t6, $s4, $t4
            ble $t6, 1, imprimir_lista
            li $t7, 0                           # $t7 = j
        loop_interno_bubble_sort:
            addi $t8, $t7, 1
            bge $t8, $t6, fim_loop_interno
            mul $t9, $t7, 4
            mul $t0, $t8, 4
            
            add $a0, $t9, $t3
            add $a1, $t0, $t3
            
            l.s $f1, 0($a0)
            l.s $f2, 0($a1)
            c.le.s $f1, $f2
            bc1t no_swap
            s.s $f1, 0($a1)
            s.s $f2, 0($a0)
            
            mul $t9, $t7, 4
            mul $t0, $t8, 4
            
            add $a0, $t1, $t9
            add $a1, $t1, $t0
            
            lw $t9, 0($a0)
            lw $t0, 0($a1)
            
            sw $t0, 0($a0)
            sw $t9, 0($a1)
            
            li $t5, 1
            
            no_swap:
                addi $t7, $t7, 1
                j loop_interno_bubble_sort
        
        fim_loop_interno:
            addi $t4, $t4, 1
            bne $t5, 0, bubble_sort
        
    imprimir_lista:
        la $t0, str_tabela_lista
        li $t4, 0
        li $v0, 4
        la $a0, str_organizar
        syscall
        loop_impressao:
            beq $t4, $s4, menu_principal
            la $a0, quebra
            syscall
            mul $t5, $t4, 4
            add $t6, $t0, $t5
            lw $a0, 0($t6)
            syscall
            
            add $t6, $t1, $t5
            lw $t6, 0($t6)
            mul $t6, $t6, 4
            add $t6, $t6, $t2
            lw $a0, 0($t6)
            syscall
            
            addi $t4, $t4, 1
            j loop_impressao
        
exit:
    li $v0, 10
    syscall